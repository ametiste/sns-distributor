allprojects {

    version = "0.1.0-SNAPSHOT"

    repositories {
        mavenCentral()
    }

}

subprojects {

    apply plugin: 'maven'
    apply plugin: 'java'

    sourceCompatibility = 1.8

    group = 'org.ametiste'

    ext {

        springVersion = '4.1.6.RELEASE'
        springBootVersion = '1.2.4.RELEASE'
        elasticsearchVersion = '6.1.0'

    }

    configurations.all {
        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }


    dependencies {
        testCompile 'junit:junit:4.8.2'
        testCompile 'org.mockito:mockito-all:1.9.5'
        compile "org.slf4j:slf4j-api:1.7.7"
    }

    configurations {
        deployerJars
    }

    javadoc {
        options {
            encoding = 'UTF-8'
        }
    }

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'


    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    test.outputs.upToDateWhen {false}
    test.testLogging.showStandardStreams = true

    test {
        afterTest { desc, result ->
            println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
        }
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

configure(rootProject) {

    description = 'Sns distributors'

}
